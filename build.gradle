plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.4'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.sonarqube' version '5.1.0.4882'
	id 'checkstyle'
}

group = 'co.edu.uniquindio'
version = '0.0.1-SNAPSHOT'
description = 'VitalCareBack'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-authorization-server'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	testImplementation 'org.springframework.security:spring-security-test'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.mysql:mysql-connector-j'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	// MapStruct
	implementation 'org.mapstruct:mapstruct:1.5.5.Final'

	// Procesador de anotaciones (necesario para generar los mappers)
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

	// Si usas Lombok (opcional pero recomendado)
	compileOnly 'org.projectlombok:lombok:1.18.32'
	annotationProcessor 'org.projectlombok:lombok:1.18.32'

	// JWT (JJWT)
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
	implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'

	implementation 'org.springframework.boot:spring-boot-starter-mail'

	implementation 'org.postgresql:postgresql:42.7.4'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'com.sendgrid:sendgrid-java:4.10.3'

	//Prometheus y Grafana
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'io.micrometer:micrometer-registry-prometheus'

    // In-memory database for tests
    testImplementation 'com.h2database:h2:2.1.214'

}

tasks.named('test') {
	useJUnitPlatform()
}

// --- Documentation and code style enforcement ---
// Fail build on missing Javadoc using doclint and treat warnings as errors
tasks.withType(Javadoc).configureEach {
	options.encoding = 'UTF-8'
	options.addBooleanOption('Xdoclint:all', true)
	options.addBooleanOption('Werror', true)
}

// Configure Checkstyle with a ruleset focused on Javadoc requirements
checkstyle {
	toolVersion = '10.17.0'
	config = resources.text.fromFile('config/checkstyle/checkstyle.xml')
	// Stop the build on any violation (missing Javadoc counts as violation)
	ignoreFailures = false
}

tasks.register('checkstyleAll') {
	group = 'verification'
	description = 'Runs Checkstyle on main and test source sets.'
	dependsOn tasks.named('checkstyleMain'), tasks.named('checkstyleTest')
}

sonar {
	properties {
		property 'sonar.projectKey', 'VitalCare-Back'
		property 'sonar.projectName', 'VitalCare-Back'
		// property 'sonar.host.url', 'http://localhost:9000' // Provided by withSonarQubeEnv in Jenkins
		// property 'sonar.login', System.getenv('SONAR_AUTH_TOKEN')
		property 'sonar.java.binaries', file("${buildDir}/classes")
	}
}
